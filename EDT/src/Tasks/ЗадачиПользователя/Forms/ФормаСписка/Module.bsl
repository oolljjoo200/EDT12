&НаСервереБезКонтекста
Процедура ОбновитьСтатусЗадач(МассивЗадач, НовыйСтатус)

	Для каждого ТекЗадача Из МассивЗадач Цикл
	
		ЗадачаОбъект = ТекЗадача.ПолучитьОбъект();
		Если ЗадачаОбъект.Статус = НовыйСтатус Тогда
			Продолжить;
		КонецЕсли;
		
		ЗадачаОбъект.Статус = НовыйСтатус;
		
		Если НовыйСтатус = Перечисления.СтатусВыполнение.Завершено Тогда
			ЗадачаОбъект.Выполнена = Истина;
		КонецЕсли;                                                
		
		Попытка
			ЗадачаОбъект.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не удалось изменить статус задачи " + ТекЗадача + " по причине: " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры // ()

&НаКлиенте
Процедура ОбновитьСтатусКлиента(МассивЗадач, НовыйСтатус)

	Если ТипЗнч(МассивЗадач) = Тип("Массив") Тогда
		ОбновитьСтатусЗадач(МассивЗадач, НовыйСтатус);
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("ЗадачаСсылка.ЗадачиПользователя")); 
КонецПроцедуры // ()


&НаКлиенте
Процедура СписокНужноВыполнитьПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)  
	ОбновитьСтатусКлиента(ПараметрыПеретаскивания.Значение, ПредопределенноеЗначение("Перечисление.СтатусВыполнение.НужноСделать")); 
КонецПроцедуры

&НаКлиенте
Процедура СписокВРаботеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ОбновитьСтатусКлиента(ПараметрыПеретаскивания.Значение, ПредопределенноеЗначение("Перечисление.СтатусВыполнение.ВРаботе")); 
КонецПроцедуры

&НаКлиенте
Процедура СписокТестированиеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ОбновитьСтатусКлиента(ПараметрыПеретаскивания.Значение, ПредопределенноеЗначение("Перечисление.СтатусВыполнение.Тестирование"));
КонецПроцедуры

&НаКлиенте
Процедура СписокГотовоПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле) 
	ОбновитьСтатусКлиента(ПараметрыПеретаскивания.Значение, ПредопределенноеЗначение("Перечисление.СтатусВыполнение.Завершено"));
КонецПроцедуры
